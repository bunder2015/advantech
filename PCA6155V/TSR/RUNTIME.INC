; CONSTANTS
CR      EQU 13              ; CARRIAGE RETURN
LF      EQU 10              ; LINE FEED
NEWLN   EQU CR,LF,DST       ; NEW LINE
NEWLN2  EQU CR,LF,CR,LF,DST ; DOUBLE NEW LINE
PSPENV  EQU 2CH             ; PSP ENVIRONMENT BLOCK
DST     EQU 36              ; $ TERMINATOR
ZST     EQU 0               ; 0 TERMINATOR

; STRINGS
CTRLERR DB "ERROR: CONTROLLER INTERRUPT IN USE, CANNOT INSTALL TSR!",NEWLN2
INTRO   DB "ADVANTECH PCA-6155V WATCHDOG DRIVER - V0.2 SEP 20 2020",NEWLN2
OUTRO   DB "TSR: INSTALLING TSR...",NEWLN2

; VARIABLES
OLD98H  DD ?

; RUNTIME INCLUDES
INCLUDE ..\COMMON\DOSVER.INC 

MAIN:
; PRINT THE INTRO STRING
    MOV DX,OFFSET INTRO
    MOV AH,09H
    INT 21H

; CHECK DOS VERSION
    CALL DOSVER

; GET CONTROLLER INTERRUPT 98H
    CLI

    MOV AX,3598H
    INT 21H

; CHECK IF UNUSED
    CMP BX,0000H
    JNE ERR_CTRLUSED
    MOV AX,ES
    CMP AX,0000H
    JNE ERR_CTRLUSED

; SET CONTROLLER INTERRUPT 98H
    MOV DX,OFFSET INT98H
    MOV AX,2598H
    INT 21H

; GET TIMER INTERRUPT 08H
    MOV AX,3508H
    INT 21H

    MOV WORD PTR DS:OLD08H,BX
    MOV WORD PTR DS:OLD08H+2,ES

; SET TIMER INTERRUPT 08H
    MOV DX,OFFSET INT08H
    MOV AX,2508H
    INT 21H

    STI

; PRINT THE OUTRO STRING
    MOV DX,OFFSET OUTRO
    MOV AH,09H
    INT 21H

; FREE ENVIRONMENT FROM PSP
    MOV ES,CS:PSPENV
    MOV AH,49H
    INT 21H

; DETERMINE MEMORY TO KEEP THEN GO RESIDENT
    MOV DX,OFFSET ENDRESIDENT
    SUB DX,OFFSET ENTRY
    ADD DX,100H                 ; PSP SEGMENT
    ADD DX,0FH                  ; PADDING FOR DIVISION
    SHR DX,04H                  ; DIVIDE BY 16

    MOV AX,3100H
    INT 21H

ERR_CTRLUSED:
    STI
; PRINT ERROR AND EXIT
    MOV DX,OFFSET CTRLERR
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H
