; CONSTANTS
PORT    EQU 443H    ; IO PORT OF WATCHDOG
TSKIP   EQU 6       ; TIMER TICKS SKIPPED BETWEEN WATCHDOG UPDATES (6*(1/18.2MS)=0.329S)

; VARIABLES
OLD08H  DD ?        ; OLD INT 08H VECTOR
TIME    DB 00H      ; WATCHDOG TIMEOUT IN SECONDS (1-63)
TTICK   DB 00H      ; CURRENT SKIP TICK

INT08H:
; INTERRUPT 08H HANDLER - SYSTEM TIMER
    PUSHA
    PUSHF

; CHECK IF WATCHDOG TIMEOUT IS SET
    CMP CS:TIME,00H
    JE ENDINT08H

; CHECK TO SEE IF WE SHOULD UPDATE THE WATCHDOG
    DEC CS:TTICK
    JNZ ENDINT08H

; RESET SKIP TICK
    MOV CS:TTICK,TSKIP

; UPDATE WATCHDOG
    MOV AL,CS:TIME
    MOV DX,PORT
    OUT DX,AL

ENDINT08H:
    POPF
    POPA

; JUMP TO OLD VECTOR
    JMP FAR [CS]:OLD08H

;;;

INT98H:
; INTERRUPT 98H HANDLER - WATCHDOG CONTROLLER
; THIS SEQUENCE LETS THE COMMAND LINE UTILITY KNOW WE ARE RUNNING
    CMP AX,"HE"
    JNE .W
    CMP BX,"LO"
    JNE .W

    MOV AX,"IM"
    MOV BX,"OK"

    JMP ENDINT98H

.W:
; THIS SEQUENCE RETURNS THE CURRENT WATCHDOG TIMEOUT IN BL
    CMP AX,"WT"         ; WATCHDOG TIMEOUT
    JNE .R

    MOV BL,CS:TIME

    JMP ENDINT98H
.R:
; THIS SEQUENCE STARTS THE WATCHDOG TIMEOUT WITH THE VALUE IN BL ON THE NEXT TICK
    CMP AX,"RW"         ; RUN WATCHDOG
    JNE .S

    MOV CS:TIME,BL
    MOV CS:TTICK,01H

    JMP ENDINT98H
.S:
; THIS SEQUENCE STOPS THE WATCHDOG AND SETS THE TIMEOUT TO 0
    CMP AX,"SW"         ; STOP WATCHDOG
    JNE .B

    MOV DX,[WORD PTR CS:PORT]
    IN AL,DX

    MOV CS:TIME,00H
    MOV CS:TTICK,00H

    JMP ENDINT98H
.B:
; THIS SEQUENCE STOPS THE WATCHDOG AND TICKS IT ONCE TO MAKE THE MACHINE REBOOT
    CMP AX,"RB"     ; REBOOT
    JNE .E

    MOV DX,[WORD PTR CS:PORT]
    IN AL,DX

    MOV CS:TIME,00H
    MOV CS:TTICK,00H

    MOV AL,01H
    MOV DX,PORT
    OUT DX,AL

    JMP ENDINT98H
.E:
; MORE SEQUENCE HANDLERS HERE?

; THIS SEQUENCE RETURNS AN ERROR BACK TO THE COMMAND LINE UTILITY
    MOV AX,"WH"
    MOV BX,"AT"

; RETURN TO COMMAND LINE UTILITY
ENDINT98H:
    IRET

ENDRESIDENT:    ; NO TSR CODE PAST THIS POINT
