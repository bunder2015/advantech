; CONSTANTS
ARGLEN  EQU 80H ; LENGTH OF ARGUMENTS IN PSP
ARGS    EQU 81H ; BEGINNING OF ARGUMENTS IN PSP

; STRINGS
HELP1   DB "CMD ERROR: WD.COM NN (1-63 SECONDS)",CR,LF,CR,LF,DST

CMDLINE:
; GET LENGTH OF ARGUMENTS AND PREPARE TO PARSE
    MOV CL,CS:ARGLEN
    CMP CL,00H  ; EMPTY
    JZ NOARGS

    CMP CL,01H  ; SINGLE CHARACTER
    JE ARGERR

    CMP CL,04H  ; FOUR OR MORE CHARACTERS
    JAE ARGERR

    SUB CL,01H  ; REMOVE SPACE
    CMP CL,01H  ; SINGLE CHARACTER
    JE ONECHAR

TWOCHAR:
; CONVERT TWO CHARACTER NUMBER INTO SINGLE BYTE NUMBER
    MOV BH,CS:ARGS+1
    CMP BH,30H  ; ZERO CHARACTER
    JB ARGERR
    CMP BH,39H  ; NINE CHARACTER
    JA ARGERR
    SUB BH,30H  ; REMOVE ASCII ENCODING

    MOV BL,CS:ARGS+2
    CMP BL,30H  ; ZERO CHARACTER
    JB ARGERR
    CMP BL,39H  ; NINE CHARACTER
    JA ARGERR
    SUB BL,30H  ; REMOVE ASCII ENCODING

    MOV AL,0AH
    MUL BH      ; MULTIPLY HIGH DIGIT BY 10
    ADD AL,BL   ; ADD LOW DIGIT

    CMP AL,00H  ; CHECK LOWER BOUNDS (1 MIN)
    JE ARGERR

    CMP AL,3FH  ; CHECK UPPER BOUNDS (63 MAX)
    JA ARGERR

    MOV CS:TIME,AL  ; SET WATCHDOG TIMEOUT

    RET

ONECHAR:
; CONVERT CHARACTER NUMBER INTO SINGLE BYTE NUMBER
    MOV AL,CS:ARGS+1
    CMP AL,30H  ; ZERO CHARACTER
    JB ARGERR
    CMP AL,39H  ; NINE CHARACTER
    JA ARGERR
    SUB AL,30H  ; REMOVE ASCII ENCODING

    CMP AL,00H  ; CHECK LOWER BOUNDS (1 MIN)
    JE ARGERR

    CMP AL,09H  ; CHECK UPPER BOUNDS (9 MAX)
    JA ARGERR

    MOV CS:TIME,AL  ; SET WATCHDOG TIMEOUT

    RET

ARGERR:
; PRINT ERROR MESSAGE AND QUIT
    MOV DX,OFFSET HELP1
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H

NOARGS:
; RETURN TO RUNTIME
    RET
