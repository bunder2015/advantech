; CONSTANTS
ARGLEN  EQU 80H ; LENGTH OF ARGUMENTS IN PSP
ARGS    EQU 81H ; BEGINNING OF ARGUMENTS IN PSP

; STRINGS
HELP    DB "ERROR: INVALID COMMAND",NEWLN
HELP1   DB "  WDCTRL [NN|/W|/S|/R]",NEWLN
HELP2   DB "    NN      START WATCHDOG, TIMEOUT IN SECONDS (1-63)",NEWLN
HELP3   DB "    /W      GET CURRENT WATCHDOG TIMEOUT",NEWLN
HELP4   DB "    /S      STOP WATCHDOG",NEWLN
HELP5   DB "    /R      REBOOT MACHINE WITH WATCHDOG",NEWLN

; VARIABLES
ATIME   DW  ?       ; TIME IN ASCII FORMAT
HTIME   DB  ?       ; TIME IN HEX FORMAT

CMDLINE:
; GET LENGTH OF ARGUMENTS
    MOV CL,CS:ARGLEN

    CMP CL,01H          ; ZERO OR ONE CHARACTER
    JBE .BA1

    CMP CL,04H          ; FOUR OR MORE CHARACTERS
    JAE .BA1

; CHECK IF FIRST CHARACTER IS A SPACE
    MOV DL,CS:ARGS
    
    CMP DL," "
    JNE .BA1

; CHECK IF WE ARE PARSING ONE CHARACTER OR TWO
    SUB CL,01H          ; REMOVE SPACE
    CMP CL,02H
    JE TWOCHAR

; CHECK SINGLE CHARACTER FOR 1-9
    MOV CL,CS:ARGS+1
    CMP CL,"?"
    JE .HE1
    CMP CL,"1"
    JB .BA1
    CMP CL,"9"
    JA .BA1

; STORE ASCII ENCODED NUMBER
    MOV BYTE PTR CS:ATIME,00H        ; CLEAR UPPER DIGIT
    MOV BYTE PTR CS:ATIME+1,CL

; REMOVE ASCII ENCODING AND STORE HEX NUMBER
    SUB CL,30H
    MOV CS:HTIME,CL

; SEND COMMAND TO WATCHDOG
    JMP RUNWD

.BA1:
    JMP ERR_BADARGS

.HE1:
    JMP HELP_ARGS

TWOCHAR:
    MOV DL,CS:ARGS+1

    CMP DL,"/"          ; CHECK FOR SWITCHES
    JE SWITCHES

; CHECK FIRST CHARACTER FOR 1-9
    MOV CH,CS:ARGS+1
    CMP CH,"1"
    JB ERR_BADARGS
    CMP CH,"9"
    JA ERR_BADARGS

; STORE ASCII ENCODING
    MOV BYTE PTR CS:ATIME,CH

; CHECK SECOND CHARACTER FOR 0-9, OR ? FOR HELP
    MOV BYTE PTR CL,CS:ARGS+2
    CMP CL,"?"
    JE HELP_ARGS
    CMP CL,"0"
    JB ERR_BADARGS
    CMP CL,"9"
    JA ERR_BADARGS    

; STORE ASCII ENCODING
    MOV BYTE PTR CS:ATIME+1,CL

; REMOVE ASCII ENCODING
    SUB CH,30H
    SUB CL,30H

; MERGE ASCII BYTES AND STORE HEX NUMBER
    MOV AL,0AH
    MUL CH          ; MULTIPLY LEFT DIGIT BY 10
    ADD CL,AL       ; ADD TO LOWER DIGIT
    MOV CS:HTIME,CL

; CHECK MERGED NUMBER FOR 1-63
    CMP CL,00H
    JE ERR_BADARGS
    CMP CL,3FH
    JA ERR_BADARGS

; SEND COMMAND TO WATCHDOG
    JMP RUNWD

SWITCHES:
    MOV DL,CS:ARGS+2

    CMP DL,"W"      ; GET WATCHDOG TIMEOUT COMMAND
    JNE .SW1

    JMP GETWT
.SW1:
    CMP DL,"S"      ; STOP WATCHDOG COMMAND
    JNE .SW2

    JMP STOPWD
.SW2:
    CMP DL,"R"      ; REBOOT MACHINE WITH WATCHDOG COMMAND
    JNE .SW3

    JMP REBOOTWD
.SW3:
    CMP DL,"?"      ; HELP
    JNE .SW4

    JMP HELP_ARGS
.SW4:
; MORE COMMANDS HERE

ERR_BADARGS:
; PRINT ERROR AND HELP
    MOV DX,OFFSET HELP
    MOV AH,09H
    INT 21H

HELP_ARGS:
; PRINT HELP AND EXIT
    MOV DX,OFFSET NEWLINE
    MOV AH,09H
    INT 21H

    MOV DX,OFFSET HELP1
    MOV AH,09H
    INT 21H

    MOV DX,OFFSET HELP2
    MOV AH,09H
    INT 21H

    MOV DX,OFFSET HELP3
    MOV AH,09H
    INT 21H

    MOV DX,OFFSET HELP4
    MOV AH,09H
    INT 21H

    MOV DX,OFFSET HELP5
    MOV AH,09H
    INT 21H

    MOV DX,OFFSET NEWLINE
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H
