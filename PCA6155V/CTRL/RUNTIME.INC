; CONSTANTS
CR      EQU 13              ; CARRIAGE RETURN
LF      EQU 10              ; LINE FEED
NEWLN   EQU CR,LF,DST       ; NEW LINE
NEWLN2  EQU CR,LF,CR,LF,DST ; DOUBLE NEW LINE
DST     EQU 36              ; $ TERMINATOR
ZST     EQU 0               ; 0 TERMINATOR

; STRINGS
BADCMD  DB "ERROR: TSR DID NOT ACCEPT COMMAND!",NEWLN2
BADINT  DB "ERROR: TSR FAILED HANDSHAKE!",NEWLN2
INTRO   DB "ADVANTECH PCA-6155V WATCHDOG CONTROLLER - V0.2 SEP 20 2020",NEWLN2
NEWLINE DB NEWLN
NOTSR   DB "ERROR: TSR IS NOT LOADED!",NEWLN2
OKTSR   DB "TSR: OK",NEWLN
TSRINFO DB "INFO: CHECKING TSR STATUS...",NEWLN

; VARIABLES
INT98H  DD ?    ; CURRENT INT 98H VECTOR

; RUNTIME INCLUDES
INCLUDE CMDLINE.INC
INCLUDE COMMANDS.INC
INCLUDE ..\COMMON\DOSVER.INC

MAIN:
; PRINT THE INTRO STRING
    MOV DX,OFFSET INTRO
    MOV AH,09H
    INT 21H

; CHECK DOS VERSION
    CALL DOSVER

; CHECK TSR STATUS
    MOV DX,OFFSET TSRINFO
    MOV AH,09H
    INT 21H

; GET CONTROLLER INTERRUPT 98H
    CLI

    MOV AX,3598H
    INT 21H

    MOV WORD PTR DS:INT98H,BX
    MOV WORD PTR DS:INT98H+2,ES

    STI

; CHECK IF UNUSED
    CMP WORD PTR INT98H,0000H
    JE ERR_NOTSR
    CMP WORD PTR INT98H+2,0000H
    JE ERR_NOTSR

; CHECK IF THIS IS OUR 98H HANDLER
    MOV AX,"HE"
    MOV BX,"LO"
    INT 98H

    CMP AX,"IM"
    JNE ERR_BADINT
    CMP BX,"OK"
    JNE ERR_BADINT

; PRINT SUCCESS
    MOV DX,OFFSET OKTSR
    MOV AH,09H
    INT 21H

; PARSE COMMAND LINE AND PROCESS COMMANDS
    JMP CMDLINE

ENDMAIN:
; PRINT NEWLINE
    MOV DX,OFFSET NEWLINE
    MOV AH,09H
    INT 21H

; EXIT TO DOS
    MOV AX,4C00H
    INT 21H

CMDCHECK:
; CHECK IF TSR PROCESSED OUR COMMAND
    CMP AX,"WH"
    JE ERR_BADCMD
    CMP BX,"AT"
    JE ERR_BADCMD

; PRINT SUCCESS
    PUSHA
    
    MOV DX,OFFSET OKTSR
    MOV AH,09H
    INT 21H
    
    POPA
    RET

ERR_BADCMD:
; PRINT ERROR AND EXIT
    MOV DX,OFFSET BADCMD
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H

ERR_BADINT:
; PRINT ERROR AND EXIT
    MOV DX,OFFSET BADINT
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H

ERR_NOTSR:
; PRINT ERROR AND EXIT
    MOV DX,OFFSET NOTSR
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H
