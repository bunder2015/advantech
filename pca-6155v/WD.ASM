P286
.MODEL TINY
.CODE
    ORG 100H
ENTRY:
    JMP MAIN

OLDVEC  DD ?        ; OLD TIMER IRQ VECTOR
PORT    EQU 443H    ; IO PORT OF WATCHDOG
PULSES  EQU 8       ; TIMER TICKS BETWEEN WATCHDOG PULSES (8*0.055S=0.44S)
TICK    DB PULSES   ; CURRENT TIMER TICK
TIME    DB 5        ; SECONDS (IN HEX) BEFORE WATCHDOG RESETS

TICKWD:
; TICK THE WATCHDOG EVERY (PULSES * 0.055) SECONDS
    PUSHA
    PUSHF

    DEC CS:TICK
    JNZ JUMPOLD
    MOV CS:TICK,PULSES

    MOV AL,CS:TIME
    MOV DX,PORT
    OUT DX,AL

JUMPOLD:
    POPF
    POPA

; JUMP TO OLD VECTOR
    JMP FAR [CS]:OLDVEC

ENDTSRCODE:     ; NO TSR CODE PAST THIS POINT

INTRO   DB "ADVANTECH PCA-6155V WATCHDOG DRIVER - V0.1 AUG 14 2020",13,10,36
LOCKF   DB "C:\PCA6155V\WD.LCK",0   ; NAME OF LOCKFILE
LOCKH   DB ?    ; LOCKFILE HANDLE
NOLOCK1 DB "LOCKFILE EXISTS, TSR NOT INSTALLED!",13,10,13,10,36
NOLOCK2 DB "ERROR CREATING LOCKFILE, TSR NOT INSTALLED!",13,10,13,10,36
OKLOCK  DB "TSR INSTALLED.",13,10,13,10,36

LOCKFILE:
; CHECK FOR EXISTING LOCKFILE
    MOV DX,OFFSET LOCKF
    MOV AX,3D00H
    INT 21H

    JNC LOCKERR1

; LOCKFILE DID NOT EXIST, CREATE ONE
    MOV DX,OFFSET LOCKF
    MOV CX,00H
    MOV AH,3CH
    INT 21H

    JC LOCKERR2

; CLOSE FILE HANDLE
    MOV LOCKH,AL
    MOV BX,WORD PTR [LOCKH]
    MOV AH,3EH
    INT 21H

; PRINT LOCK SUCCESS
    MOV DX,OFFSET OKLOCK
    MOV AH,09H
    INT 21H

    RET

LOCKERR1:
; EXISTING FILE - CLOSE FILE HANDLE, PRINT ERROR, EXIT
    MOV LOCKH,AL
    MOV BX,WORD PTR [LOCKH]
    MOV AH,3EH
    INT 21H

    MOV DX,OFFSET NOLOCK1
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H

LOCKERR2:
; CREATE ERROR - PRINT ERROR, EXIT
    MOV DX,OFFSET NOLOCK2
    MOV AH,09H
    INT 21H

    MOV AX,4C02H
    INT 21H

SETUP:
; GET VECTOR OF INT 08H, STORE
    MOV AX,3508H
    INT 21H

    MOV WORD PTR [OLDVEC+2],ES
    MOV WORD PTR [OLDVEC],BX

; SET VECTOR OF INT 08H TO TICKWD
    MOV DX,OFFSET TICKWD
    MOV AX,2508H
    INT 21H

; STOP WATCHDOG IF IT IS CURRENTLY RUNNING
    MOV DX,PORT
    IN AL,DX

; SET WATCHDOG TO INTERVAL
    MOV AL,TIME
    MOV DX,PORT
    OUT DX,AL

    RET

MAIN:
; PRINT THE INTRO
    MOV DX,OFFSET INTRO
    MOV AH,09H
    INT 21H

    CALL LOCKFILE
    CALL SETUP

GOTSR:
; FREE ENVIRONMENT FROM PSP
    MOV ES,CS:2CH       ; SEGMENT ADDRESS OF ENVIRONMENT BLOCK
    MOV AH,49H
    INT 21H

; DETERMINE MEMORY TO KEEP AND GO RESIDENT
    MOV DX,OFFSET ENDTSRCODE    ; END OF CODE
    SUB DX,OFFSET ENTRY         ; BEGINNING OF CODE
    ADD DX,100H                 ; PSP SEGMENT
    ADD DX,0FH                  ; PADDING FOR DIVISION
    SHR DX,04H                  ; DIVIDE BY 16

    MOV AX,3100H
    INT 21H

ERROR:
; WE SHOULD NEVER GET HERE, EXIT NOW
    MOV AX,4C02H
    INT 21H

END ENTRY
