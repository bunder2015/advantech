P286
.MODEL TINY
.CODE
    ORG 100H
ENTRY:
    JMP MAIN

PORT    EQU 443H    ; IO PORT OF WATCHDOG
OLDVEC  DD ?        ; OLD TIMER IRQ VECTOR
TIME    DB 5        ; SECONDS (IN HEX) BEFORE WATCHDOG RESETS

TICKWD:
; TICK THE WATCHDOG
    PUSH AX
    PUSH DX

    MOV AL,TIME
    MOV DX,PORT
    OUT DX,AL

    POP DX
    POP AX

; JUMP TO OLD VECTOR
    JMP FAR [CS]:[OLDVEC]

ENDTSRCODE:     ; NO TSR CODE PAST THIS POINT

INTRO   DB "ADVANTECH PCA-6155V WATCHDOG DRIVER - V0.1 AUG 14 2020",13,10,36
LOCKF   DB "C:\PCA6155V\WD.LCK",0   ; NAME OF LOCKFILE
LOCKH   DB ?    ; LOCKFILE HANDLE
NOLOCK  DB "UNABLE TO ACQUIRE LOCK, TSR NOT INSTALLED",13,10,13,10,36
OKLOCK  DB "TSR INSTALLED",13,10,13,10,36

LOCKFILE:
; CHECK FOR EXISTING LOCKFILE
    MOV DX,OFFSET LOCKF
    MOV AH,3DH
    MOV AL,00H
    INT 21H

    MOV LOCKH,AL
    JNC LOCKERR

; CLOSE FILE HANDLE IF WE GOT ONE
    MOV BX,WORD PTR [LOCKH]
    MOV AH,3EH
    INT 21H

; LOCKFILE DID NOT EXIST, CREATE ONE
    MOV DX,OFFSET LOCKF
    MOV CX,00H
    MOV AH,3CH
    INT 21H

    MOV LOCKH,AL
    JC LOCKERR

; CLOSE FILE HANDLE IF WE GOT ONE
    MOV BX,WORD PTR [LOCKH]
    MOV AH,3EH
    INT 21H

; PRINT LOCK SUCCESS
    MOV DX,OFFSET OKLOCK
    MOV AH,09H
    INT 21H

    RET

LOCKERR:
; EXISTING LOCK FILE OR COULD NOT CREATE, PRINT ERROR AND EXIT
    MOV DX,OFFSET NOLOCK
    MOV AH,09H
    INT 21H

    MOV AH,4CH
    MOV AL,02H
    INT 21H

SETUP:
; GET VECTOR OF INT 08H, STORE
    MOV AH,35H
    MOV AL,08H
    INT 21H

    MOV WORD PTR [OLDVEC+2],ES
    MOV WORD PTR [OLDVEC],BX

; SET VECTOR OF INT 08H TO TICKWD
    MOV DX,OFFSET TICKWD
    MOV AH,25H
    MOV AL,08H
    INT 21H

; STOP WATCHDOG IF IT IS CURRENTLY RUNNING
    MOV DX,PORT
    IN AL,DX

; SET WATCHDOG TO INTERVAL
    MOV AL,TIME
    MOV DX,PORT
    OUT DX,AL

    RET

MAIN:
; PRINT THE INTRO
    MOV DX,OFFSET INTRO
    MOV AH,09H
    INT 21H

    CALL LOCKFILE
    CALL SETUP

GOTSR:
; FREE ENVIRONMENT FROM PSP
    MOV ES,CS:[002CH]   ; SEGMENT ADDRESS OF ENVIRONMENT BLOCK
    MOV AH,49H
    INT 21H

; DETERMINE MEMORY TO KEEP AND GO RESIDENT
    MOV DX,OFFSET ENDTSRCODE    ; END OF CODE
    SUB DX,OFFSET ENTRY         ; BEGINNING OF CODE
    ADD DX,100H                 ; PSP SEGMENT
    ADD DX,10H                  ; PADDING FOR DIVISION
    SHR DX,04H                  ; DIVIDE BY 16

    MOV AH,31H
    MOV AL,00H
    INT 21H

ERROR:
; WE SHOULD NEVER GET HERE, EXIT NOW
    MOV AH,4CH
    MOV AL,02H
    INT 21H

END ENTRY
